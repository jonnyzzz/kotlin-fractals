apply plugin: "kotlin-platform-js"
apply plugin: "org.jetbrains.kotlin.frontend"

dependencies {
  expectedBy project(":common")

  compile "org.jetbrains.kotlinx:kotlinx-html-js:$kotlin_html_version"
  compile "org.jetbrains:kotlin-react-dom:$react_dom_version"
  compile "org.jetbrains:kotlin-styled:$styled_version"

  compile "org.jetbrains:kotlin-react:$react_version"
  compile "org.jetbrains:kotlin-css-js:$css_version"


  compile "org.jetbrains.kotlin:kotlin-stdlib-js"
  compile "org.jetbrains.kotlin:kotlin-test-js"
}

kotlin {
  experimental.coroutines = 'ENABLE'
}

tasks.withType(org.jetbrains.kotlin.gradle.tasks.Kotlin2JsCompile) {
  kotlinOptions.metaInfo = true
  kotlinOptions.sourceMap = true
  kotlinOptions.moduleKind = "commonjs"
  kotlinOptions.main = "call"
}

kotlinFrontend {
  npm {
    //color spaces
    dependency "color-convert"
    dependency "react"
    dependency "react-dom"
    dependency "@jetbrains/kotlin-css"
    dependency "@jetbrains/kotlin-css-js"
    dependency "@jetbrains/kotlin-react"
    dependency "@jetbrains/kotlin-react-dom"

    devDependency("karma")
    devDependency("qunit")
  }

  sourceMaps = true

  webpackBundle {
    bundleName = "main"
    contentPath = file('src/main/web')
    port = 7777
  }

  define "PRODUCTION", false
}

task('run_Qunit', type: Exec) {
  dependsOn "compileTestKotlin2Js"

  commandLine "node",
          "node_modules/qunit/bin/qunit",
          "classes/kotlin/test/js_test.js"

  environment "NODE_PATH", "$buildDir/classes/kotlin/main:$buildDir/classes/kotlin/test:$buildDir/node_modules"

  workingDir(buildDir)
}

//TODO: make runner connect incrementals
project.afterEvaluate {
  tasks.getByName("karma-run-single").outputs.upToDateWhen { false }
  tasks.getByName("webpack-bundle").dependsOn tasks.compileKotlin2Js, tasks.compileTestKotlin2Js
}

